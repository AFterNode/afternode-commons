plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'cn.afternode'
version = '1.3'

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "java"
    apply plugin: "signing"

    group = parent.group
    version = parent.version

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven (MavenPublication) {
                group project.group
                artifactId "afternode-commons-${project.name}"
                version project.version
                from components.java

                pom {
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://github.com/AFterNode/afternode-commons/blob/main/LICENSE"
                        }
                    }

                    developers {
                        developer {
                            id = "h3xadecimal"
                            name = "H3xadecimal"
                            email = "h3xadecimal@afternode.cn"
                        }
                    }

                    scm {
                        url = "https://github.com/AFterNode/afternode-commons"
                    }
                }
            }
        }

        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign configurations.archives
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}
